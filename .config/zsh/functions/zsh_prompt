#!/bin/zsh

setopt prompt_subst

_prompt_apple () {
  [ "$(uname)" = "Darwin" ] && [ -n "$TERM_PROGRAM" ] && echo '%F{white}%f '
}

_prompt_pwd () {
  echo "${PWD/#$HOME/~}" | sed -e 's-\(\.*[^/]\)[^/]*/-\1/-g'
}

_prompt_git_status () {
  local __git_branch="$(git rev-parse --abbrev-ref HEAD 2>/dev/null)"
  if [ -n "$__git_branch" ]; then
    echo -n "on %F{cyan}$__git_branch"
    local __git_toplevel="$(git rev-parse --show-toplevel 2>/dev/null)"
    if [ -n "$__git_toplevel" ]; then
      echo -n '%F{red}'
      [ -n "$(git status --porcelain --untracked-files=no --ignore-submodules=dirty)" ] && echo -n '!'
      [ -n "$(timeout .2 git ls-files --other --exclude-standard -- "$__git_toplevel")" ] && echo -n '?'
    fi
    echo '%f '
  fi
}

_prompt_virtual_env () {
  [ -n "$VIRTUAL_ENV" ] && echo "(%F{blue}$(basename $VIRTUAL_ENV)%f) "
}

_prompt_indicator () {
  git rev-parse 2>/dev/null && ! { [ "$(git rev-parse --show-toplevel 2>/dev/null)" = "$HOME" ] && git check-ignore --quiet "$PWD" 2>/dev/null; } && echo '±' && return
  echo '%(!.√.↳)'
}

zsh_prompt () {
  PROMPT='%F{yellow}%n%f at $(_prompt_apple)%F{magenta}%m%f in %F{blue}${$(_prompt_pwd)//\//"%F{red}/%F{blue}"}%f $(_prompt_git_status)
$(_prompt_virtual_env)$(_prompt_indicator) '
}

zsh_prompt
